# 手动修改栈帧数据

see https://gitbook.coder.cat/function-call-principle/content/c-stack-frame-example.html

我们可以获取某一局部变量在内存中的地址，然后对该地址进行偏移来修改栈中其他位置的数据。只要我们对栈结构足够了解，就可以修改你想修改的数据。

## C语言函数栈帧实例

```c
long callee(long arg1, long arg2, long arg3, long arg4, long arg5, long arg6, long arg7, long arg8) {
    return arg7 + arg8;
}

int main() {
    long a = 7;
    long b = 8;
    callee(1, 2, 3, 4 ,5 ,6, a, b);
    return 0;
}
```

代码中callee函数有8个参数分别是arg1~arg8，它返回arg7和arg8相加后的结果。由于我们的代码是运行在X64的机器上，所以arg1~arg6会通过寄存器来传递，arg7和arg8通过栈来传递。main中定义了两个局部变量a和b，它对callee发起调用，局部变量a,b分别对应callee函数的arg7,arg8。

将上面C语言代码编译成汇编，把由编译器产生的其他与我们分析函数栈帧不相关的指令删除，得到如下指令（根据编译器版本以及操作系统的不同编译出的汇编指令会有所差异，这里使用的编译器和操作系统分别是gcc9.0和ubuntu 20.04 x-64）：
```asm
callee:
    pushq    %rbp
    movq    %rsp, %rbp
    movq    %rdi, -8(%rbp)
    movq    %rsi, -16(%rbp)
    movq    %rdx, -24(%rbp)
    movq    %rcx, -32(%rbp)
    movq    %r8, -40(%rbp)
    movq    %r9, -48(%rbp)
    movq    16(%rbp), %rdx
    movq    24(%rbp), %rax
    addq    %rdx, %rax
    popq    %rbp
    ret
main:
    pushq    %rbp
    movq    %rsp, %rbp
    subq    $16, %rsp
    movq    $7, -16(%rbp)
    movq    $8, -8(%rbp)
    pushq    -8(%rbp)
    pushq    -16(%rbp)
    movl    $6, %r9d
    movl    $5, %r8d
    movl    $4, %ecx
    movl    $3, %edx
    movl    $2, %esi
    movl    $1, %edi
    call    callee
    addq    $16, %rsp
    movl    $0, %eax
    leave
    ret
```
指令中的callee:和main:表示的是为它下面的指令取一个名字，可以理解为函数名。

在X64中栈指针和栈基址分别叫rsp,rbp。

### 我们逐步分析这些指令对堆栈的影响，首先是main的第前两行指令：
```asm
pushq    %rbp
movq    %rsp, %rbp
```

实际上main函数是被系统内一个叫做_start的函数所调用，所以第1条指令将调用者_start的栈基址压栈保存，第2条指令移动栈基址使它指向和栈指针同样的位置。

栈变化过程（在一开始我们栈指针和栈基址指向_start的栈帧，这里没有画出_start的栈帧也就没有标注它们）：

**initial**
**pushq    %rbp**
- rsp -> __start rbp
**movq    %rsp, %rbp**
- rbp, rsp -> __start rbp

### 初始化局部变量并准备callee的后两个参数：
```asm
subq    $16, %rsp
movq    $7, -16(%rbp)
movq    $8, -8(%rbp)
pushq    -8(%rbp)
pushq    -16(%rbp)
```
第1条指令中的subq是减法指令，这里用于把栈指针减去16，使它向低地址移动来给局部变量a,b分配16字节的空间（long占8个字节）。

第2~3条指令中的movq是数据移动指令，在这里它通过对栈基址进行适当偏移来对a,b赋值。例如movq $7, -16(%rbp)表示把7放到相对于栈基址-16的内存中。下图中可以看到这两条指令执行完以后-16和-8的位置刚好放置了变量a,b。

第4~5条指令把栈帧中a,b的值复制一份然后把它们压栈，这是为了准备函数callee的后2个参数arg7与arg8。

栈变化过程（图中右侧的数字表示基于栈基址的偏移量，每个格子8字节，由于栈空间是向低地址发展的，所以相对于栈基址上面内存的偏移量是负的）：

**subq    $16, %rsp**
- rsp ->
- 
- rbp -> __start rbp
**movq    $7, -16(%rbp)**
- rsp ->  a
-
- rbp -> __start rbp
**movq    $8, -8(%rbp)**
- rsp ->  a
- b
- rbp -> __start rbp
**pushq    -8(%rbp)**
- rsp ->  arg8
- a
- b
- rbp -> __start rbp
**pushq    -16(%rbp)**
- rsp ->  arg7
- arg8
- a
- b
- rbp -> __start rbp

### 把callee的前6个参数arg1~arg6存放在寄存器中：
```asm
movl    $6, %r9d
movl    $5, %r8d
movl    $4, %ecx
movl    $3, %edx
movl    $2, %esi
movl    $1, %edi
```
### 开始调用callee：
```asm
call    callee
```
这条指令对应了两个操作，首先将它下面一条指令的地址也就是addq $16, %rsp的地址（返回地址）压入栈中，随后修改程序计数器（PC）为callee的第一条指令的地址，最后CPU就从callee处开始执行。

这条指令执行后的栈：
**call    callee**
- rsp ->  ret addr
- arg7
- arg8
- a
- b
- rbp -> __start rbp

### 现在CPU从callee开始执行：
```asm
pushq    %rbp
movq    %rsp, %rbp
```

先压入main栈帧的栈基址，然后移动栈基址使它指向和栈指针同样的位置。此时产生了callee的栈帧：

第2条指令使栈基址发生了改变，相应的右图中基于栈基址的偏移量也需要发生变化（由于栈空间是向低地址发展的，所以相对于栈基址下面内存的偏移量是正的）：

**pushq    %rbp**
- rsp ->  main rbp
- ret addr
- arg7
- arg8
- a
- b
- rbp -> __start rbp

**movq    %rsp, %rbp**
- rbp,rsp ->  main rbp
- ret addr
- arg7
- arg8
- a
- b
- __start rbp

### 复制前6个参数到栈帧中：
```asm
movq    %rdi, -8(%rbp)
movq    %rsi, -16(%rbp)
movq    %rdx, -24(%rbp)
movq    %rcx, -32(%rbp)
movq    %r8, -40(%rbp)
movq    %r9, -48(%rbp)
```
这6个寄存器rdi,rsi,rdx,rcx,r8,r9分别存放了callee的前6个参数，现在将它们从寄存器中复制到callee自己的栈帧中。

**执行之后的栈结构**
- arg1
- arg2
- arg3
- arg4
- arg5
- arg6
- rbp,rsp ->  main rbp
- ret addr
- arg7
- arg8
- a
- b
- __start rbp

执行到这里奇怪的事情发生了，栈指针并没有继续向低地址移动，让它指向栈顶的位置。其实这里是编译器的优化，栈指针的目的是为了确定被调用函数的栈基址，由于callee没有再继续调用其他任何函数，因此也无需修改栈指针。

### 执行相加：
```asm
movq    16(%rbp), %rdx
movq    24(%rbp), %rax
addq    %rdx, %rax
```
将arg7与arg8相加，然后把结果放在rdx寄存器，mian函数可以访问rdx寄存器来获取返回值，以此来达到函数返回值传递的目的。

### 返回到main：
```asm
popq    %rbp
ret
```
恢复栈基址并跳转到返回地址处开始继续执行。
栈变化过程：
**popq    %rbp**
- arg1
- arg2
- arg3
- arg4
- arg5
- arg6
- main rbp
- rsp -> ret addr
- arg7
- arg8
- a
- b
- rbp -> __start rbp
**ret**
- arg1
- arg2
- arg3
- arg4
- arg5
- arg6
- main rbp
- ret addr
- rsp -> arg7
- arg8
- a
- b
- rbp -> __start rbp


## 修改局部存储
由于C/C++不检查数组越界访问，因此我们可以借助这一特点来修改栈帧中的数据。

有如下代码：
```c
int main() {
    long foo = 10;
    long arr[2] = {1, 2};
    arr[-1] = 20;
    printf("foo is %ld", foo);
    return 0;
}
```
代码中数组arr和变量foo它们在栈中是连续存放的

变量foo放在了更低的地址上，所以在修改arr[-1]时也是修改变量foo。

代码输出到控制中的结果：
```c
foo is 20
```

## stack canary

这个例子的栈帧最底部多了一个canary（译为金丝雀），它用来防止栈溢出攻击。在进入函数时会先往返回地址的上面放置一个任意值，然后在返回时检查这个值是否有被修改，如果被修改就说明栈帧中其他数据也可能被修改过，然后触发相关异常。这个值就叫canary。

只有在函数内有指针操作时，编译器才会生成canary相关指令。我们这里使用了数组，数组操作在C/C++中其实也是指针操作，所以这里生成了canary。

修改返回地址
同样的方式我们也可以对一个内存地址进行适当偏移来修改函数的返回地址，从而控制函数返回路径。

有如下代码：
```c
void bar() {
    printf("This is bar!\n");
}

void foo() {
    printf("This is foo!\n");
    long arr[2] = {1, 2, 3};
    arr[5] = bar;
}

int main() {
    foo();
    return 0;
}
```
这个例子中在数组arr[5]的位置存放了返回地址，至于为什么是5，我们先来看下在foo返回前的栈帧结构（canary 以下是main的栈帧，其他是foo的栈帧）：
```out
arr[0]
arr[1]
arr[2]
main rbp
canary
ret addr
start rbp
```
现在我们从arr[0]往下数，刚好在arr[5]的位置放置了函数返回地址，我们把它修改成函数bar的地址（C/C++中函数名表示函数第一条指令的地址）然后执行，于是得到如下控制台结果：
```out
This is foo!
This is bar!
段错误 (核心已转储)
```
结果如我们所愿这里成功跳转到了bar，但也收获了一个段错误。为什么会发生这个段错误呢？我们继续看下在bar运行前后的栈帧变化（绿色是main的栈帧，蓝色是bar的栈帧）：
```out
bar addr
start rbp
```
在ret指令执行前的栈帧状态，这个时候栈指针和栈基址已经恢复到调用foo前的状态，右图是在ret指令执行后，此时bar addr已经被出栈，随后跳转到bar去执行，bar也创建了自己的栈帧。这个时候我们想像一下，当bar返回时会发生什么？显然它会把_start rbp做为返回地址，_start rbp不是一个指令的地址，当CPU把它当成一个指令地址去寻找指令时，就发生了段错误。
